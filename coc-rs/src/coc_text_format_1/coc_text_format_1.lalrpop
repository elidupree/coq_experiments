use crate::coc_text_format_1::{Command,Formula,Abstraction,AbstractionKind};

grammar;

extern {
    type Error = String;
}

pub Command: Command = {
  <Name> ":=" <Formula> => Command::Assign(<>),
  <Formula> ":" <Formula> => Command::ClaimType(<>),
}

Formula: Formula = {
  "ℙ" => Formula::Prop,
  "_" => Formula::Hole,
  Name => Formula::Usage(<>),
  <kind:AbstractionKind> <parameter_name:Name> ":" <parameter_type:Formula> "," <body:Formula>
    => Formula::Abstraction(Box::new(Abstraction{<>})),
  "(" <ApplyChain> ")" => <>,
}

ApplyChain: Formula = {
  ApplyChain Formula => Formula::Apply(Box::new([<>])),
  Formula => <>,
}

Name: String = r"[A-Za-z][a-zA-Z0-9]*" => <>.to_string();
AbstractionKind: AbstractionKind = {
  "λ" => AbstractionKind::Lambda,
  "∀" => AbstractionKind::ForAll,
}
