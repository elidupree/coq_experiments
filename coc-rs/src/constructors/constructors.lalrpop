use crate::constructors::{Constructors, ConstructorEntry, DataArgument, Precondition, DataValue};
use lalrpop_util::ParseError;

grammar;

extern {
    type Error = String;
}

pub Constructors: Constructors = {
  <entries:Entry*> =>? {
    let mut constructors = Constructors::default();
    for entry in entries.into_iter().flatten() {
      constructors.add_entry(entry).map_err(|error| ParseError::User {error})?;
    }
    Ok(constructors)
  }
}

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None=> v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

Entry: Option<ConstructorEntry> = {
  r"/\*[^\*]*\*/" => None,

  "data" <name:TypeName> ";" => Some(ConstructorEntry::DataType{name}),

  "predicate" <name:TypeName> <type_parameters:TypeName+> "," "notated" <notation:Notation> ";"
    => Some(ConstructorEntry::PredicateType{<>}),

  <name:TypeName> "(" <data_arguments:Comma<DataArgument>> <preconditions:Comma<Precondition>> ")"
    "->" <constructed_type:TypeName> <resulting_type_parameters:DataValue*> <notation:("," "notated" <Notation>)?> ";"
    => Some(ConstructorEntry::Constructor{<>}),
}

DataArgument: DataArgument = <name:ArgumentName> ":" <datatype:TypeName> => DataArgument {<>};
Precondition: Precondition = "_" ":" <predicate_type:TypeName> <type_parameters:DataValue+> => Precondition {<>};
DataValue: DataValue = {
  <name:ArgumentName> => DataValue::Argument(name),
  <constructor:TypeName> => DataValue::Constructor{<>, arguments: Vec::new()},
  "("<constructor:TypeName> <arguments:DataValue*>")" => DataValue::Constructor{<>},
}

TypeName: String = r"[A-Z][a-zA-Z0-9]*" => <>.to_string();
ArgumentName: String = r"[a-z][a-zA-Z0-9_]*" => <>.to_string();
Notation: String = r#""[^"]*""# => <>.trim_matches('"').to_string();
